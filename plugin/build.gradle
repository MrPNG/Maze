import org.ajoberstar.grgit.Grgit
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext.kotlin_version = '1.2.51'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id "org.ajoberstar.grgit" version "1.7.2"
}

apply plugin: 'kotlin'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        suppressWarnings = true
    }
}

ext {
    git = Grgit.open(file('.'))
    revision = git.head().getAbbreviatedId(7)
}

//Force resources processing only when shadowing jar
processResources.outputs.upToDateWhen { !gradle.taskGraph.hasTask(shadowJar) }

processResources {
    filter ReplaceTokens, tokens: [
            'pl.name'   : project.name,
            'pl.group'  : project.group,
            'pl.version': project.version + ' (Built at ' + timestamp() + ' from revision ' + revision + ')',
            'pl.authors': 'MrPingu_'
    ]
}

shadowJar {
    classifier = null
    version = null
}

task copyJar(type: Copy) {
    from shadowJar
    into 'I:\\Programming\\Spigot Servers\\Dusty\\plugins'
}

repositories {
    jcenter()
    mavenLocal()

    maven { url "https://repo.viaversion.com/" }
    maven { url "https://jitpack.io/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compileOnly "org.spigotmc:spigot:1.8.8-R0.1-SNAPSHOT"

    compileOnly "com.github.MrPNG:ProtocolSupport:master-SNAPSHOT"
    compileOnly "us.myles:viaversion:1.3.0"
}

static def timestamp() {
    return new Date().format("yyyy-MM-ddTHH:mm:ssZ", TimeZone.getDefault())
}
